////////////////////////////////////////////////////////////////////////////////
//  PLUGIN CONFIGURATION
////////////////////////////////////////////////////////////////////////////////
plugins {
    id "io.ratpack.ratpack-groovy" version "0.9.10"
    id 'com.jfrog.bintray' version '1.0'
}

apply plugin: 'groovy'
////////////////////////////////////////////////////////////////////////////////

version = '0.2.0'
group = 'offtopic'
description =' Offtopic is a simple web application built with Ratpack for inspecting and consuming events from Kafka.'
sourceCompatibility = '1.8'
targetCompatibility = '1.8'


////////////////////////////////////////////////////////////////////////////////
//  DEPENDENCY AND REPOSITORY MANAGEMENT
////////////////////////////////////////////////////////////////////////////////
repositories {
    mavenLocal()

    jcenter()
    mavenCentral()
}

dependencies {
    compile 'io.ratpack:ratpack-handlebars:0.9.10+'
    compile 'io.ratpack:ratpack-jackson:0.9.10+'
    compile 'io.ratpack:ratpack-codahale-metrics:0.9.10+'
    compile localGroovy()

    /* Needed for discovering brokers and all kinds of other things in
     * Zookeeeper
     */
    compile 'org.apache.curator:curator-framework:2.7.+'
    compile 'org.apache.commons:commons-pool2:2.2+'

    compile 'org.apache.kafka:kafka_2.10:0.8.1.1+'
    // Forcing us up to ZK 3.5 to prevent wacky classpath errors when mixing
    // and matching dependencies
    compile 'org.apache.zookeeper:zookeeper:3.5.+'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'cglib:cglib-nodep:2.2.+'
}
////////////////////////////////////////////////////////////////////////////////

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

////////////////////////////////////////////////////////////////////////////////
//  RELEASING
////////////////////////////////////////////////////////////////////////////////
bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'rtyler'
        repo = 'maven'
        name = 'offtopic'
        labels = ['kafka', 'offtopic', 'ratpack', 'groovy']

        version {
            name = project.version
            vcsTag = "v${project.version}"
            desc = project.description
        }
    }
}
bintrayUpload.dependsOn assemble

////////////////////////////////////////////////////////////////////////////////
// vim: ft=groovy
