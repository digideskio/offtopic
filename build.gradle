////////////////////////////////////////////////////////////////////////////////
//  PLUGIN CONFIGURATION
////////////////////////////////////////////////////////////////////////////////
buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:[1.3.1,1.4)'
        classpath 'io.ratpack:ratpack-gradle:[0.9.19,1.0)'
        classpath 'com.github.jengelman.gradle.plugins:shadow:[1.2.2,1.3)'
    }
}

apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'io.ratpack.ratpack-groovy'
apply plugin: 'com.github.johnrengelman.shadow'
////////////////////////////////////////////////////////////////////////////////

version = '0.2.0'
group = 'offtopic'
description =' Offtopic is a simple web application built with Ratpack for inspecting and consuming events from Kafka.'

defaultTasks 'check', 'assemble'

////////////////////////////////////////////////////////////////////////////////
//  DEPENDENCY AND REPOSITORY MANAGEMENT
////////////////////////////////////////////////////////////////////////////////
repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:[2.4.4,2.5)'

    compile 'io.ratpack:ratpack-handlebars:[0.9.19,1.0)'
    compile 'io.ratpack:ratpack-jackson:[0.9.17,1.0)'
    compile 'io.ratpack:ratpack-codahale-metrics:[0.9.19,1.0)'

    /* Needed for discovering brokers and all kinds of other things in
     * Zookeeeper
     */
    compile 'org.apache.curator:curator-framework:[2.7.1,2.8)'
    compile 'org.apache.commons:commons-pool2:[2.2,3.0)'

    /* For logging at runtime */
    compile 'ch.qos.logback:logback-parent:[1.1.3,2.0)'

    compile 'org.apache.kafka:kafka_2.11:0.8.2.1'
    // Forcing us up to ZK 3.5 to prevent wacky classpath errors when mixing
    // and matching dependencies
    compile 'org.apache.zookeeper:zookeeper:3.5.1-alpha'

    testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
    testCompile 'cglib:cglib-nodep:[2.2.2,2.3)'
}
////////////////////////////////////////////////////////////////////////////////

test {
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
}

run {
    systemProperty 'zookeepers', 'localhost:2181'
    dependsOn check
}

assemble.dependsOn shadowJar

////////////////////////////////////////////////////////////////////////////////
//  RELEASING
////////////////////////////////////////////////////////////////////////////////
bintray {
    user = project.bintrayUser
    key = project.bintrayKey
    publish = true
    dryRun = false
    configurations = ['archives']

    pkg {
        userOrg = 'rtyler'
        repo = 'maven'
        name = 'offtopic'
        labels = ['kafka', 'offtopic', 'ratpack', 'groovy']

        version {
            name = project.version
            vcsTag = "v${project.version}"
            desc = project.description
        }
    }
}
bintrayUpload.dependsOn assemble

////////////////////////////////////////////////////////////////////////////////

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// vim: ft=groovy
